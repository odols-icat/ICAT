**----------------------------------------------------------------------*
****INCLUDE LZRM_VALORACIO_OFERTESCL6.
**----------------------------------------------------------------------*
CLASS lcl_alv_0850_util DEFINITION FINAL.
***********************************************************************************************
**                                         PUBLIC SECTION                                     *
***********************************************************************************************
  PUBLIC SECTION.
    TYPES: BEGIN OF ty_s_columns,
             fieldname              TYPE char250,
             z_comptador_equip      TYPE zrm_dt_punt_incr-z_comptador_equip,
             z_carrec               TYPE zrm_dt_punt_incr-z_carrec,
             z_imin                 TYPE zrm_dt_punt_incr-z_imin,
             z_incr                 TYPE zrm_dt_punt_incr-z_incr,
             z_punts                TYPE zrm_dt_punt_incr-z_punts,
             z_singularitat         TYPE zrm_dt_punt_incr-z_singularitat,
             z_actual_in_bottom_alv TYPE xfeld,
           END OF ty_s_columns.
    TYPES: ty_t_columns TYPE STANDARD TABLE OF ty_s_columns.
    TYPES: BEGIN OF ty_s_formula,
             formula TYPE string,
             z_incr  TYPE zrm_dt_punt_incr-z_incr,
             z_imin  TYPE zrm_dt_punt_incr-z_imin,
           END OF ty_s_formula.
    TYPES: ty_t_formula TYPE STANDARD TABLE OF ty_s_formula.

    CONSTANTS: co_cell_color TYPE string  VALUE 'CELL_COLOR',
               co_cell_style TYPE string  VALUE 'CELL_STYLE'.

    DATA: my_changes_done         TYPE xfeld VALUE abap_off,
          gt_fcat                 TYPE lvc_t_fcat,
          gr_cont_incr            TYPE REF TO cl_gui_custom_container,
          gr_alv_incr             TYPE REF TO cl_gui_alv_grid,
          my_puntuacions_inicials TYPE zrm_cl_valoracio_ofertes=>ty_t_punt_gen,
          my_punts                TYPE zrm_cl_valoracio_ofertes=>ty_t_punt_gen,
          my_suapar_do_punts      TYPE zrm_dm_suapar_do, "ETODR SPEC-55535
          my_tt_suapar_do_punts   TYPE STANDARD TABLE OF zrm_dm_suapar_do, "ETODR SPEC-55535
          my_tt_formula           TYPE ty_t_formula,
          my_readonly             TYPE xfeld,
          my_incr                 TYPE ztt_punt_incr,
          my_incr_bd              TYPE ztt_punt_incr,
          my_company_name         TYPE string,
          my_num_oferta           TYPE z_num_ofer,
          my_singularitat(2)      TYPE c,
          my_formula              TYPE z_formula_incr,
          my_punt_max_do          TYPE z_punt_max_do.



    METHODS constructor
      IMPORTING
        it_incr      TYPE ztt_punt_incr
        it_puntuacio TYPE zrm_cl_valoracio_ofertes=>ty_t_punt_gen
        i_readonly   TYPE xfeld.

    METHODS create_alv.
    METHODS show_alv.
    METHODS handle_incr_changed FOR EVENT data_changed OF cl_gui_alv_grid IMPORTING er_data_changed.
    METHODS set_layout EXPORTING es_layout   TYPE lvc_s_layo.
    METHODS fill_catalog EXPORTING et_fieldcat TYPE lvc_t_fcat.
    METHODS user_command IMPORTING i_ucomm TYPE syst_ucomm.
    METHODS free_variables.
    METHODS check_incr_introduida RETURNING VALUE(r_incr_ok) TYPE xfeld.
    METHODS get_flag_changes RETURNING VALUE(r_changes) TYPE xfeld.
    METHODS set_flag_changes IMPORTING i_changes TYPE xfeld.
    METHODS modif_incr_puntuacions CHANGING er_data_changed TYPE REF TO cl_alv_changed_data_protocol.
    METHODS get_puntuacions EXPORTING et_incr       TYPE ztt_punt_incr
                                      et_puntuacio  TYPE zrm_cl_valoracio_ofertes=>ty_t_punt_gen
                                      et_no_changes TYPE xfeld.
    METHODS aplicar_formula IMPORTING im_formula TYPE z_formula_incr
                            CHANGING  cs_incr    TYPE zrm_dt_punt_incr.
ENDCLASS.
*
*
CLASS lcl_alv_0850_util IMPLEMENTATION.
***********************************************************************************************
**                                         PUBLIC SECTION                                     *
***********************************************************************************************
**---------------------------------------------------------------------*
** METHOD CONSTRUCTOR
**---------------------------------------------------------------------*
** Mètode constructor.
**---------------------------------------------------------------------*
  METHOD constructor.
    DATA: ls_incr TYPE zrm_dt_punt_incr,
          ls_ofer TYPE zrm_dt_punt_ofer.

    my_incr = it_incr.
    my_incr_bd = it_incr.
    my_puntuacions_inicials = it_puntuacio.
    my_punts = it_puntuacio.
    my_readonly = i_readonly.

    READ TABLE it_incr INTO ls_incr INDEX 1.

    my_num_oferta = ls_incr-z_num_ofer.

*-- Nom sencer del proveidor ------------------------------------
    SELECT SINGLE proveidor
    FROM zrm_dt_ofertes
    INTO @DATA(lv_lifnr)
          WHERE objectid    = @ls_incr-objectid
          AND docclass    = @ls_incr-docclass
          AND z_num_ofer  = @ls_incr-z_num_ofer.

    IF sy-subrc = 0 AND lv_lifnr IS NOT INITIAL.
      SELECT SINGLE name1,name2,name3,name4
      FROM lfa1
      INTO (@DATA(l_name1),@DATA(l_name2),@DATA(l_name3),@DATA(l_name4))
            WHERE lifnr EQ @lv_lifnr.

      CONCATENATE l_name1 l_name2 l_name3 l_name4 INTO my_company_name  SEPARATED BY space.
      CONDENSE my_company_name.
    ENDIF.

    READ TABLE my_incr ASSIGNING FIELD-SYMBOL(<fs_incr>) INDEX 1.
    CHECK sy-subrc EQ 0.


    SELECT SINGLE *
      INTO ls_ofer
      FROM zrm_dt_punt_ofer
     WHERE docclass         EQ <fs_incr>-docclass
       AND objectid         EQ <fs_incr>-objectid
       AND z_mod_crit_punt  EQ <fs_incr>-z_mod_crit_punt
       AND z_codi_crit_punt EQ <fs_incr>-z_codi_crit_punt.

    SELECT SINGLE z_formula_incr z_punt_max
      INTO (my_formula, my_punt_max_do)
      FROM zrm_dm_suapar_pt
     WHERE z_codi_subapar_punt_rel  EQ ls_ofer-z_codi_subapar_punt.

    SELECT SINGLE z_singularitat
      INTO my_singularitat
      FROM zrm_dm_suapar_do
     WHERE z_codi_subapar_punt  EQ ls_ofer-z_codi_subapar_punt AND
           z_singularitat EQ 'SI'.

    SELECT SINGLE *
      INTO my_suapar_do_punts
      FROM zrm_dm_suapar_do
     WHERE z_codi_subapar_punt  EQ ls_ofer-z_codi_subapar_punt AND
           z_singularitat       EQ 'SI'.

    SELECT *
      INTO TABLE my_tt_suapar_do_punts
      FROM zrm_dm_suapar_do
     WHERE z_codi_subapar_punt  EQ ls_ofer-z_codi_subapar_punt.

    IF my_readonly IS INITIAL.
      "Sempre que entrem recalculem per si s'ha canviat alguna cosa del IMIN
      DATA(lt_punts_before) = my_punts.
      LOOP AT my_incr  ASSIGNING FIELD-SYMBOL(<fs_incr_inicial>).
        LOOP AT my_punts ASSIGNING FIELD-SYMBOL(<fs_punt_inici>) WHERE z_num_gen         = my_num_oferta
                                                                  AND  z_codi_crit_punt  = <fs_incr_inicial>-z_codi_crit_punt.
          aplicar_formula( EXPORTING im_formula = my_formula
                           CHANGING  cs_incr    = <fs_incr_inicial> ).
          ">>>SPEC-52548, Inici modificació  - ETODR
          "Al canviar si aplica o no Singularitat a "Seleccionar de nou?" s'ha de replicar aquí
          IF my_singularitat = abap_off AND <fs_incr_inicial>-z_singularitat IS NOT INITIAL.
            CLEAR: <fs_incr_inicial>-z_singularitat.
          ENDIF.
          "<<<SPEC-52548, Final modificació - ETODR
          IF <fs_incr_inicial>-z_singularitat IS NOT INITIAL.
            <fs_incr_inicial>-z_punts = <fs_incr_inicial>-z_punts + my_suapar_do_punts-z_punt_max_do.
          ENDIF.
          <fs_punt_inici>-z_punts = <fs_incr_inicial>-z_punts.
        ENDLOOP.
      ENDLOOP.
      IF lt_punts_before <> my_punts.
        set_flag_changes( abap_on ).
      ENDIF.
    ENDIF.

  ENDMETHOD.
*---------------------------------------------------------------------*
* METHOD SHOW_ALV
*---------------------------------------------------------------------*
* Mètode que mostra la ALV.
*---------------------------------------------------------------------*
  METHOD create_alv.
    CALL SCREEN '0850' STARTING AT 10 10.
  ENDMETHOD.

*---------------------------------------------------------------------*
* METHOD SHOW_ALV
*---------------------------------------------------------------------*
* Mètode que mostra la ALV.
*---------------------------------------------------------------------*
  METHOD show_alv.

    DATA: lt_exclude TYPE ui_functions,
          wa_exclude TYPE ui_func.

    IF gr_cont_incr IS INITIAL.
      CREATE OBJECT gr_cont_incr
        EXPORTING
          container_name = 'CC_ALV_INCR_EQUIP'.

      CHECK gr_cont_incr IS BOUND.

      CREATE OBJECT gr_alv_incr
        EXPORTING
          i_parent = gr_cont_incr
        EXCEPTIONS
          OTHERS   = 0.
      CHECK gr_alv_incr IS BOUND.

* --------------------------------------------------------------------*
** Aplicar el manegador del hotspot.
      SET HANDLER gr_util_incr->handle_incr_changed FOR gr_alv_incr.
      gr_alv_incr->register_edit_event( EXPORTING i_event_id = cl_gui_alv_grid=>mc_evt_modified ).
* --------------------------------------------------------------------*

      set_layout( IMPORTING es_layout = DATA(ls_layout) ).
      fill_catalog( IMPORTING et_fieldcat = DATA(lt_fcat) ).


      CLEAR: lt_exclude[].
      wa_exclude = cl_gui_alv_grid=>mc_fc_excl_all.
      APPEND wa_exclude TO lt_exclude.


      gr_alv_incr->set_table_for_first_display( EXPORTING  is_layout                     = ls_layout
        it_toolbar_excluding          = lt_exclude
      CHANGING   it_outtab            = my_incr
        it_fieldcatalog               = lt_fcat
      EXCEPTIONS invalid_parameter_combination = 1
        program_error                 = 2
        too_many_lines                = 3
        OTHERS                        = 4 ).

      CHECK sy-subrc = 0.

      IF my_readonly = abap_on.
        gr_alv_incr->set_ready_for_input( EXPORTING i_ready_for_input = 0 ).
      ELSE.
        gr_alv_incr->set_ready_for_input( EXPORTING i_ready_for_input = 1 ).
      ENDIF.
    ELSE.
      gr_alv_incr->refresh_table_display( ).
    ENDIF.
  ENDMETHOD.
*---------------------------------------------------------------------*
* METHOD GET_FLAG_CHANGES
*---------------------------------------------------------------------*
* Mètode que retorna si s'han realitzat canvis
*---------------------------------------------------------------------*
  METHOD get_flag_changes.
    r_changes = my_changes_done.
  ENDMETHOD.

*---------------------------------------------------------------------*
* METHOD SET_FLAG_CHANGES
*---------------------------------------------------------------------*
* Mètode que determina si s'han realitzat canvis
*---------------------------------------------------------------------*
  METHOD set_flag_changes.
    my_changes_done = i_changes.
  ENDMETHOD.

*---------------------------------------------------------------------*
* METHOD GET_PDM_PUNTUACIONS
*---------------------------------------------------------------------*
* Mètode que retorna les puntuacions del criteri de PDM per tècnic
*---------------------------------------------------------------------*
  METHOD modif_incr_puntuacions.
    DATA: wa_incr  TYPE zrm_dt_punt_incr,
          lv_punts TYPE f.

    READ TABLE er_data_changed->mt_good_cells ASSIGNING FIELD-SYMBOL(<fs_good>)
     INDEX 1.
    CHECK sy-subrc EQ 0.

    READ TABLE my_incr ASSIGNING FIELD-SYMBOL(<fs_incr>) INDEX <fs_good>-row_id.
    CHECK sy-subrc EQ 0.

    LOOP AT my_punts ASSIGNING FIELD-SYMBOL(<fs_punt>)
      WHERE z_num_gen = my_num_oferta
        AND z_codi_crit_punt  = <fs_incr>-z_codi_crit_punt.

      CHECK sy-subrc EQ 0.

      IF <fs_good>-fieldname = 'Z_SINGULARITAT'.
        IF <fs_good>-value = abap_true.
          "Ha d'acumular a PUNTS el valor de la formula de IMINvsINCR + SINGULARITAT
          <fs_incr>-z_punts = <fs_incr>-z_punts + my_suapar_do_punts-z_punt_max_do.
        ELSE.
          aplicar_formula( EXPORTING im_formula = my_formula
                           CHANGING  cs_incr    = <fs_incr> ).
        ENDIF.
      ELSE.
        <fs_incr>-z_incr = <fs_good>-value.
        aplicar_formula( EXPORTING im_formula = my_formula
                         CHANGING  cs_incr    = <fs_incr> ).
        IF <fs_incr>-z_singularitat = abap_on.
          <fs_incr>-z_punts = <fs_incr>-z_punts + my_suapar_do_punts-z_punt_max_do.
        ENDIF.
      ENDIF.
      <fs_punt>-z_punts = <fs_incr>-z_punts.

    ENDLOOP.
  ENDMETHOD.

*---------------------------------------------------------------------*
* METHOD SET_LAYOUT
*---------------------------------------------------------------------*
* Layout per la ALV de modificacions de càrrecs de l'equip
*---------------------------------------------------------------------*
  METHOD set_layout.
    es_layout-smalltitle = abap_off.
    es_layout-no_totline = abap_off.
    es_layout-no_toolbar = abap_off.
    es_layout-no_rowins  = abap_on.
    es_layout-no_rowmove = abap_on.
    es_layout-zebra      = abap_on.
    es_layout-edit_mode  = abap_on.
    es_layout-numc_total = abap_off.
    es_layout-col_opt    = abap_on.
    es_layout-sel_mode   = 'A'.
*    es_layout-grid_title = 'Informar dedicació ofertada'.
    es_layout-stylefname = 'STYLE'.
  ENDMETHOD.

*---------------------------------------------------------------------*
* METHOD FILL_CATALOG
*---------------------------------------------------------------------*
* Catàleg de camps per la ALV de modificacions de càrrecs de l'equip
*---------------------------------------------------------------------*
  METHOD fill_catalog.
    DATA: wa_fcat TYPE lvc_s_fcat.


    CLEAR wa_fcat.
    wa_fcat-fieldname     = 'Z_CARREC'.
    wa_fcat-tabname       = 'ZRM_DT_PUNT_INCR'.
    wa_fcat-outputlen     = 20.
    wa_fcat-just          = 'L'.
    wa_fcat-scrtext_s     = 'Càrrec'.
    wa_fcat-scrtext_m     = 'Càrrec'.
    wa_fcat-scrtext_l     = 'Càrrec'.
*    wa_fcat-convexit      = 'JTPAR'.
    APPEND wa_fcat TO et_fieldcat.

    CLEAR wa_fcat.
    wa_fcat-fieldname     = 'Z_IMIN'.
    wa_fcat-tabname       = 'ZRM_DT_PUNT_INCR'.
    wa_fcat-outputlen     = 15.
    wa_fcat-just          = 'L'.
    wa_fcat-scrtext_s     = 'IMIN'.
    wa_fcat-scrtext_m     = 'IMIN'.
    wa_fcat-scrtext_l     = 'IMIN'.
    APPEND wa_fcat TO et_fieldcat.

    CLEAR wa_fcat.
    wa_fcat-fieldname     = 'Z_INCR'.
    wa_fcat-tabname       = 'ZRM_DT_PUNT_INCR'.
    wa_fcat-ref_field     = 'Z_INCR'.
    wa_fcat-ref_table     = 'ZRM_DT_PUNT_INCR'.
    wa_fcat-outputlen     = 15.
    wa_fcat-just          = 'L'.
    wa_fcat-scrtext_s     = 'INCR'.
    wa_fcat-scrtext_m     = 'INCR'.
    wa_fcat-scrtext_l     = 'INCR'.
    wa_fcat-edit = abap_true.
    APPEND wa_fcat TO et_fieldcat.

    CLEAR wa_fcat.
    wa_fcat-fieldname     = 'Z_PUNTS'.
    wa_fcat-tabname       = 'ZRM_DT_PUNT_INCR'.
    wa_fcat-outputlen     = 5.
    wa_fcat-just          = 'L'.
    wa_fcat-scrtext_s     = 'Punt.'.
    wa_fcat-scrtext_m     = 'Puntuació'.
    wa_fcat-scrtext_l     = 'Puntuació'.
    wa_fcat-lowercase     = 'X'.
    APPEND wa_fcat TO et_fieldcat.

    CLEAR wa_fcat.
    wa_fcat-fieldname     = 'Z_SINGULARITAT'.
    wa_fcat-tabname       = 'MY_INCR'.
    wa_fcat-outputlen     = 10.
    wa_fcat-checkbox      = abap_true.
    wa_fcat-scrtext_s     = 'Singularitat'.
    wa_fcat-scrtext_m     = 'Singularitat'.
    wa_fcat-scrtext_l     = 'Singularitat'.
    IF my_singularitat EQ 'SI'.
      wa_fcat-edit = abap_true.
    ENDIF.
    APPEND wa_fcat TO et_fieldcat.
  ENDMETHOD.

*---------------------------------------------------------------------*
* METHOD HANDLE_DEDICACIO_CHANGED
*---------------------------------------------------------------------*
* Mètode per controlar els canvis en la ALV de càrrecs
*---------------------------------------------------------------------*
  METHOD handle_incr_changed.
    set_flag_changes( abap_on ).
    me->modif_incr_puntuacions( CHANGING er_data_changed = er_data_changed ).
    gr_alv_incr->refresh_table_display( ).
  ENDMETHOD.

*---------------------------------------------------------------------*
* METHOD USER_COMMAND
*---------------------------------------------------------------------*
* Mètode que realitza la acció del botó premut per l'usuari
*---------------------------------------------------------------------*
  METHOD user_command.
    DATA: l_answer TYPE c.
    CASE i_ucomm.

      WHEN 'CANC_EQUI'.
        IF get_flag_changes( ) = abap_on.
          CLEAR:l_answer.
          CALL FUNCTION 'POPUP_TO_CONFIRM'
            EXPORTING
              titlebar              = text-i01
              text_question         = text-i02
              display_cancel_button = abap_off
            IMPORTING
              answer                = l_answer.
          CHECK l_answer = 1.
*          CLEAR: my_equip_for_pdm[].
          my_incr = my_incr_bd.
          my_punts = my_puntuacions_inicials.
        ENDIF.
        CALL METHOD cl_gui_cfw=>flush.
        LEAVE TO LIST-PROCESSING AND RETURN TO SCREEN 0.
*
      WHEN 'OK_PDM'.
        IF my_readonly = abap_off.
          CALL METHOD cl_gui_cfw=>flush.
          LEAVE TO LIST-PROCESSING AND RETURN TO SCREEN 0.
        ELSE.
          IF check_incr_introduida( ) = abap_off.
            MESSAGE 'No es pot introduir una dedicació inferior a la mínima establerta ni tampoc superior al 100%' TYPE 'I'.
          ELSE.
            CALL METHOD cl_gui_cfw=>flush.
            LEAVE TO LIST-PROCESSING AND RETURN TO SCREEN 0.
          ENDIF.
        ENDIF.
      WHEN OTHERS.
        EXIT.
    ENDCASE.
  ENDMETHOD.

*---------------------------------------------------------------------*
* METHOD check_dedicacio_introduida
*---------------------------------------------------------------------*
* Mètode que comprova si la dedicació ofertada introduïda és vàlida
*---------------------------------------------------------------------*
  METHOD check_incr_introduida.
    r_incr_ok = abap_on.
    LOOP AT my_incr ASSIGNING FIELD-SYMBOL(<fs_dades_incr>) WHERE z_incr IS INITIAL.
      r_incr_ok = abap_off.
    ENDLOOP.
  ENDMETHOD.

*---------------------------------------------------------------------*
* METHOD free_variables
*---------------------------------------------------------------------*
* Mètode que allibera la memòria de les variables
*---------------------------------------------------------------------*
  METHOD free_variables.
    TRY.
        IF gr_alv_incr IS BOUND . gr_alv_incr->free( ). ENDIF.
        IF gr_cont_incr     IS BOUND . gr_cont_incr->free( ).      ENDIF.
        FREE: gr_alv_incr, gr_cont_incr.
      CATCH cx_root.
        EXIT.
    ENDTRY.
  ENDMETHOD.

  METHOD get_puntuacions.
    et_puntuacio = my_punts.
    et_incr      = my_incr.
  ENDMETHOD.

*---------------------------------------------------------------------*
* METHOD aplicar_formula
*---------------------------------------------------------------------*
* Mètode que aplica la fòrmula de IMINvsINCR
*---------------------------------------------------------------------*
  METHOD aplicar_formula.
    "INI ECDRB SPEC-64893
    DATA: ls_zrm_dm_eval_incr    TYPE zrm_dm_eval_incr,
          lv_resultat_formula_fm TYPE char5,
          lv_formula_val         TYPE string.

    "1. Consulta a taula ZRM_DM_EVAL_INCR.
    SELECT * FROM zrm_dm_eval_incr INTO ls_zrm_dm_eval_incr
      WHERE z_codi_formula_incr = im_formula.
      "1.1. Si hi han registres, fer el següent
*      lv_formula_val = CONV #( cs_incr-z_incr ).
      lv_formula_val = cs_incr-z_incr.
      SHIFT lv_formula_val LEFT DELETING LEADING space.
      REPLACE ALL OCCURRENCES OF '[INCR]' IN ls_zrm_dm_eval_incr-z_formula WITH lv_formula_val .
*      lv_formula_val = CONV #( cs_incr-z_imin ).
      lv_formula_val = CONV #( cs_incr-z_imin ).
      SHIFT lv_formula_val LEFT DELETING LEADING space.
      REPLACE ALL OCCURRENCES OF '[IMIN]' IN ls_zrm_dm_eval_incr-z_formula WITH lv_formula_val .

      CALL FUNCTION 'EVAL_FORMULA'
        EXPORTING
          formula = ls_zrm_dm_eval_incr-z_formula
          program = sy-repid
        IMPORTING
          value   = lv_resultat_formula_fm
        EXCEPTIONS
          OTHERS  = 1.
      IF sy-subrc = 0.
        IF lv_resultat_formula_fm EQ 'TRUE'.
          cs_incr-z_punts = ls_zrm_dm_eval_incr-z_punts.
          EXIT.
        ENDIF.
      ENDIF.

    ENDSELECT.


    "1.2. Si no hi han registres, continuar al case següent.
    "FI ECDRB SPEC-64893
    IF sy-subrc EQ 0.
      EXIT.
    ENDIF.

    CASE im_formula.
      WHEN 'VI01'.
        IF cs_incr-z_incr < ( '0.1' * cs_incr-z_imin ).
          cs_incr-z_punts = '0'.
        ELSEIF cs_incr-z_incr >= ( '0.1' *  cs_incr-z_imin ) AND cs_incr-z_incr < ( '0.5' * cs_incr-z_imin ).
          cs_incr-z_punts = '1.5'.
        ELSEIF cs_incr-z_incr >= ( '0.5' *  cs_incr-z_imin ) AND cs_incr-z_incr < ( cs_incr-z_imin ).
          cs_incr-z_punts = '3'.
        ELSEIF cs_incr-z_incr >= ( cs_incr-z_imin ) AND cs_incr-z_incr < ( '1.5' * cs_incr-z_imin ).
          cs_incr-z_punts = '4'.
        ELSEIF cs_incr-z_incr >= ( '1.5' * cs_incr-z_imin ) AND cs_incr-z_incr < ( '2' * cs_incr-z_imin ).
          cs_incr-z_punts = '5'.
        ELSE.
          cs_incr-z_punts = '6'.
        ENDIF.
      WHEN 'VI02'.
        IF cs_incr-z_incr < ( '0.1' * cs_incr-z_imin ).
          cs_incr-z_punts = '0'.
        ELSEIF cs_incr-z_incr >= ( '0.1' *  cs_incr-z_imin ) AND cs_incr-z_incr < ( '0.5' * cs_incr-z_imin ).
          cs_incr-z_punts = '1'.
        ELSEIF cs_incr-z_incr >= ( '0.5' *  cs_incr-z_imin ) AND cs_incr-z_incr < ( cs_incr-z_imin ).
          cs_incr-z_punts = '2'.
        ELSEIF cs_incr-z_incr >= ( cs_incr-z_imin ) AND cs_incr-z_incr < ( '1.5' * cs_incr-z_imin ).
          cs_incr-z_punts = '3'.
        ELSEIF cs_incr-z_incr >= ( '1.5' * cs_incr-z_imin ) AND cs_incr-z_incr < ( '2' * cs_incr-z_imin ).
          cs_incr-z_punts = '4'.
        ELSE.
          cs_incr-z_punts = '4.5'.
        ENDIF.
**    >> INI MODIF ETODR SPEC-63278
      WHEN 'VI03'.
        IF cs_incr-z_incr < ( '0.1' * cs_incr-z_imin ).
          cs_incr-z_punts = '0'.
        ELSEIF cs_incr-z_incr >= ( '0.1' *  cs_incr-z_imin ) AND cs_incr-z_incr < ( '0.5' * cs_incr-z_imin ).
          cs_incr-z_punts = '1'.
        ELSEIF cs_incr-z_incr >= ( '0.5' *  cs_incr-z_imin ) AND cs_incr-z_incr < ( cs_incr-z_imin ).
          cs_incr-z_punts = '2'.
        ELSEIF cs_incr-z_incr >= ( cs_incr-z_imin ) AND cs_incr-z_incr < ( '1.5' * cs_incr-z_imin ).
          cs_incr-z_punts = '3'.
        ELSEIF cs_incr-z_incr >= ( '1.5' * cs_incr-z_imin ) AND cs_incr-z_incr < ( '2' * cs_incr-z_imin ).
          cs_incr-z_punts = '3.5'.
        ELSE.
          cs_incr-z_punts = '4'.
        ENDIF.
**    << FI MODIF ETODR SPEC-63278
      WHEN OTHERS.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.