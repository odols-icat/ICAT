*&---------------------------------------------------------------------*
*&  Include           LZRM_VALORACIO_OFERTESCL2
*&---------------------------------------------------------------------*
*----------------------------------------------------------------------*
* Clase local para manejar eventos en las ALV's dynpro 0501
*----------------------------------------------------------------------*
CLASS lcl_event_handler_new DEFINITION FINAL.
  PUBLIC SECTION.
    CLASS-METHODS:
      handle_hotspot_click_0501 FOR EVENT hotspot_click OF cl_gui_alv_grid
        IMPORTING e_row_id e_column_id es_row_no,
      handle_data_changed_0501 FOR EVENT data_changed OF cl_gui_alv_grid
        IMPORTING er_data_changed sender,

      handle_toolbar_0501
                    FOR EVENT toolbar OF cl_gui_alv_grid
        IMPORTING e_object e_interactive,
      handle_user_command_0501
                    FOR EVENT user_command OF cl_gui_alv_grid
        IMPORTING e_ucomm,

      handle_toolbar_back
                    FOR EVENT toolbar OF cl_gui_alv_grid
        IMPORTING e_object e_interactive,
      handle_toolbar_next
                    FOR EVENT toolbar OF cl_gui_alv_grid
        IMPORTING e_object e_interactive.

  PRIVATE SECTION.
    CLASS-METHODS: check_set_canvi_punt_dyn CHANGING i_row_id     TYPE lvc_s_row "cr_data_changed TYPE REF TO cl_alv_changed_data_protocol
                                                     ct_puntuacio TYPE tty_puntuacio.
    CLASS-METHODS: change_seleccio_puntuacio IMPORTING i_row_id     TYPE lvc_s_row "is_data_changed TYPE lvc_s_modi
                                             CHANGING  ct_puntuacio TYPE tty_puntuacio.
    CLASS-METHODS: check_change_seleccio  IMPORTING  i_row_id       TYPE lvc_s_row
                                                     i_readonly     TYPE xfeld
                                          EXPORTING  e_nomes_un_sel TYPE xfeld
                                                     e_result       TYPE z_punt_max_do
                                          CHANGING   ct_puntuacio   TYPE tty_puntuacio
                                          EXCEPTIONS no_seleccionable.
    CLASS-METHODS: show_descripcio_llarga IMPORTING is_puntuacio TYPE gty_puntuacio. "INI MODIF ETODR SPEC-47173

ENDCLASS.                    "lcl_event_handler_new DEFINITION

*&---------------------------------------------------------------------*
*&       Class (Implementation)  lcl_event_handler_new
*&---------------------------------------------------------------------*
*----------------------------------------------------------------------*
CLASS lcl_event_handler_new IMPLEMENTATION.
  METHOD check_set_canvi_punt_dyn.
    DATA: ls_ref                     TYPE tabfield,
          l_puntuacio_actual_marcada TYPE z_punts,
          l_undo                     TYPE xfeld,
          wa_puntuacio_ant           TYPE zrm_cl_valoracio_ofertes=>ty_s_puntuacio_subapartats,
          wa_puntuacio_act           TYPE zrm_cl_valoracio_ofertes=>ty_s_puntuacio_subapartats.
    DATA: lv_num_sel TYPE z_num_sel.
*
    "Si hi ha alguna circular de validació d'OFTEC iniciada cal determinar un motiu per a realitzar cadascun dels canvis de puntuació
    "Si no es determina un motiu, es restaurarà el valor que hi havia abans del canvi
    ASSIGN COMPONENT '_CONTROL_' OF STRUCTURE <gs_grid> TO FIELD-SYMBOL(<ls_row>).
    CHECK sy-subrc = 0.

    "Base
    ASSIGN COMPONENT 'Z_CODI_CRIT_PUNT' OF STRUCTURE <ls_row> TO FIELD-SYMBOL(<lv_crit_punt>).
    CHECK sy-subrc = 0.
    ASSIGN COMPONENT 'Z_MOD_CRIT_PUNT'  OF STRUCTURE <ls_row> TO FIELD-SYMBOL(<lv_mod_punt>). "
    CHECK sy-subrc = 0.
    ASSIGN COMPONENT 'Z_DESCRIPCIO' OF STRUCTURE <ls_row> TO FIELD-SYMBOL(<lv_descripcio>).
    CHECK sy-subrc = 0.
    ASSIGN COMPONENT 'Z_PUNT_MAX'  OF STRUCTURE <ls_row> TO FIELD-SYMBOL(<lv_punt_max>). "
    CHECK sy-subrc = 0.

    IF gs_dyn_0501-g_show_all = abap_on.
      <lv_crit_punt>  = <fs>-z_codi_crit_punt.
      <lv_mod_punt>   = <fs>-z_mod_crit_punt.
      <lv_descripcio> = <fs>-z_descripcio.
      <lv_punt_max>   = <fs>-z_punt_max.
    ENDIF.

    ASSIGN COMPONENT '_PUNTS_'   OF STRUCTURE <gs_grid> TO FIELD-SYMBOL(<ls_punt>).
    CHECK sy-subrc = 0.
    CHECK <gs_column> IS ASSIGNED.

    ls_ref-tabname   = 'ZRM_DT_PUNT_OFER' .
    ls_ref-fieldname = 'Z_PUNTS' .

    LOOP AT ct_puntuacio ASSIGNING FIELD-SYMBOL(<fs_punt>) WHERE seleccio   = abap_on
                                                             AND z_opcional = abap_off.
*      DATA(l_tabix) = abap_on.
      CLEAR wa_puntuacio_ant.
      wa_puntuacio_ant-z_codi_crit_punt         = <fs_punt>-z_codi_crit_punt.
      wa_puntuacio_ant-z_codi_subapar_punt      = <fs_punt>-z_codi_subapar_punt.
      wa_puntuacio_ant-z_codi_subapar_punt_rel  = <fs_punt>-z_codi_subapar_punt_rel.
      wa_puntuacio_ant-cont_doc                 = <fs_punt>-cont_doc.
      wa_puntuacio_ant-docclass                 = <fs_punt>-docclass.
      wa_puntuacio_ant-objectid                 = <fs_punt>-objectid.
      wa_puntuacio_ant-z_num_ofer               = <fs_punt>-z_num_ofer.
      wa_puntuacio_ant-z_sec                    = <fs_punt>-z_sec.
      wa_puntuacio_ant-z_punts                  = <fs_punt>-z_punt_max_do.
      wa_puntuacio_ant-z_motivacio              = <fs_punt>-z_motivacio.
      wa_puntuacio_ant-z_comentaris             = <fs_punt>-z_comentaris.
      READ TABLE ct_puntuacio ASSIGNING FIELD-SYMBOL(<fs_punt2>) WITH KEY seleccio    = abap_on
                                                                          z_opcional  = abap_on.
      IF sy-subrc = 0.
        wa_puntuacio_ant-z_punts             = wa_puntuacio_ant-z_punts - abs( <fs_punt2>-z_punt_max_do ).
*        wa_puntuacio_ant-z_sec_resta         = wa_puntuacio_ant-z_sec_resta.
        wa_puntuacio_ant-z_motivacio_resta   = wa_puntuacio_ant-z_motivacio_resta.
        wa_puntuacio_ant-z_comentaris_resta  = wa_puntuacio_ant-z_comentaris_resta.
      ENDIF.
    ENDLOOP.
** >> INI MODIF ETODR SPEC-47173
    CLEAR: lv_num_sel.
    LOOP AT ct_puntuacio ASSIGNING FIELD-SYMBOL(<fs_punt_sel_ant>) WHERE seleccio = abap_true.
      IF <fs_punt_sel_ant>-z_opcional = abap_on.
        ADD 1 TO lv_num_sel.
      ENDIF.
      READ TABLE gt_punt_do ASSIGNING FIELD-SYMBOL(<fs_punt_do_ant>) WITH KEY z_codi_subapar_punt = <fs_punt_sel_ant>-z_codi_subapar_punt
                                                                              z_num_sel           = lv_num_sel.
      IF sy-subrc = 0.
        wa_puntuacio_ant-z_punts = <fs_punt_do_ant>-z_punts.
      ENDIF.
    ENDLOOP.
** << FI MODIF ETODR SPEC-47173

    TRY.
        READ TABLE ct_puntuacio ASSIGNING FIELD-SYMBOL(<fs_puntuacio>) INDEX i_row_id-index.
        IF sy-subrc = 0.
          CLEAR wa_puntuacio_act.
          wa_puntuacio_act-z_codi_crit_punt         = <fs_puntuacio>-z_codi_crit_punt.
          wa_puntuacio_act-z_codi_subapar_punt      = <fs_puntuacio>-z_codi_subapar_punt.
          wa_puntuacio_act-z_codi_subapar_punt_rel  = <fs_puntuacio>-z_codi_subapar_punt_rel.
          wa_puntuacio_act-cont_doc                 = <fs_puntuacio>-cont_doc.
          wa_puntuacio_act-docclass                 = <fs_puntuacio>-docclass.
          wa_puntuacio_act-objectid                 = <fs_puntuacio>-objectid.
          wa_puntuacio_act-z_num_ofer               = <fs_puntuacio>-z_num_ofer.
          wa_puntuacio_act-z_sec                    = <fs_puntuacio>-z_sec.
          wa_puntuacio_act-z_punts                  = <fs_puntuacio>-z_punt_max_do.

          IF <fs_puntuacio>-z_pun_rel_sel EQ abap_false.
**          "Si s'està modificant una puntuació cal realitzar el càlcul del resultat final del canvi en curs
            check_change_seleccio( EXPORTING  i_row_id         = i_row_id
                                              i_readonly       = abap_on
                                   IMPORTING  e_nomes_un_sel   = DATA(l_nomes_un_sel)
                                              e_result         = DATA(l_result)
                                   CHANGING   ct_puntuacio     = ct_puntuacio
                                   EXCEPTIONS no_seleccionable = 1
                                              OTHERS           = 2 ).
            IF sy-subrc = 0.
              IF l_nomes_un_sel = abap_off.
                wa_puntuacio_act-z_punts = l_result.
              ENDIF.

              wa_puntuacio_act-z_motivacio              = <fs_puntuacio>-z_motivacio.
              wa_puntuacio_act-z_comentaris             = <fs_puntuacio>-z_comentaris.
              IF wa_puntuacio_act <> wa_puntuacio_ant AND wa_puntuacio_ant IS NOT INITIAL.
                gr_backend->set_canvi_punt_dyn( EXPORTING  i_punt             = <ls_punt>
                                                           i_row              = <ls_row>
                                                           i_columns          = <gs_column>
                                                           is_ref             = ls_ref
                                                           is_puntuacio_act   = wa_puntuacio_act
                                                           is_puntuacio_ant   = wa_puntuacio_ant ).
              ENDIF.
            ENDIF.

          ELSE. "ETLJS SPEC-47173
            IF <fs_puntuacio>-z_opcional = abap_off.
              CLEAR: lv_num_sel.
            ELSE.
              IF <fs_puntuacio>-seleccio = abap_on. "Si aquí està marcat és que ara s'està esborrant
                lv_num_sel = lv_num_sel - 1.
              ELSE.
                ADD 1 TO lv_num_sel.
              ENDIF.
            ENDIF.
*            CLEAR: lv_num_sel.
*            LOOP AT ct_puntuacio ASSIGNING FIELD-SYMBOL(<fs_punt_sel>) WHERE seleccio = abap_true.
*              ADD 1 TO lv_num_sel.
            READ TABLE gt_punt_do ASSIGNING FIELD-SYMBOL(<fs_punt_do>) WITH KEY z_codi_subapar_punt = <fs_puntuacio>-z_codi_subapar_punt
                                                                                z_num_sel           = lv_num_sel.
            IF sy-subrc = 0.
              wa_puntuacio_act-z_punts = <fs_punt_do>-z_punts.
            ENDIF.
*            ENDLOOP.
            wa_puntuacio_act-z_motivacio              = <fs_puntuacio>-z_motivacio.
            wa_puntuacio_act-z_comentaris             = <fs_puntuacio>-z_comentaris.
            READ TABLE ct_puntuacio TRANSPORTING NO FIELDS WITH KEY seleccio = abap_true.
            IF sy-subrc EQ 0.
              gr_backend->set_canvi_punt_dyn( EXPORTING  i_punt             = <ls_punt>
                                                         i_row              = <ls_row>
                                                         i_columns          = <gs_column>
                                                         is_ref             = ls_ref
                                                         is_puntuacio_act   = wa_puntuacio_act
                                                         is_puntuacio_ant   = wa_puntuacio_ant ).
            ENDIF.
          ENDIF.
          change_seleccio_puntuacio(  EXPORTING i_row_id        = i_row_id
                                      CHANGING  ct_puntuacio    = ct_puntuacio ).
        ELSE.
          l_undo = abap_on.
        ENDIF.
      CATCH zcx_rm_valoracio.
        "Es desfan els canvis
        l_undo = abap_on.
    ENDTRY.

    IF l_undo <> abap_on.
      gs_dyn_0501-gv_alv_puntuacio->refresh_table_display( ).
    ENDIF.

  ENDMETHOD.
**
*  change_seleccio_puntuacio
**
  METHOD change_seleccio_puntuacio.
    DATA: lt_messages    TYPE tty_messages,
          wa_messages    TYPE ty_messages,
          l_result       TYPE z_punt_max_do,
          l_lines_sel    TYPE i,
          l_nomes_un_sel TYPE xfeld.

    CLEAR: l_result.
*
    READ TABLE ct_puntuacio ASSIGNING FIELD-SYMBOL(<fs_puntuacio>) INDEX i_row_id-index.
    CHECK sy-subrc = 0.
    CLEAR: l_lines_sel.
    LOOP AT ct_puntuacio TRANSPORTING NO FIELDS WHERE seleccio = abap_on AND z_opcional = abap_false. "SPEC-49499 ETJMD 06.06.2018
      ADD 1 TO l_lines_sel.
    ENDLOOP.
    IF l_lines_sel > 1.
      LOOP AT ct_puntuacio ASSIGNING FIELD-SYMBOL(<fs_aux>) WHERE seleccio   =  abap_on
                                                              AND z_pun_rel_sel EQ abap_false.
        CLEAR: <fs_aux>-seleccio.
      ENDLOOP.
    ENDIF.
    check_change_seleccio( EXPORTING  i_row_id       = i_row_id
                                      i_readonly     = abap_off
                           IMPORTING  e_nomes_un_sel = l_nomes_un_sel
                                      e_result       = l_result
                           CHANGING   ct_puntuacio   = ct_puntuacio
                           EXCEPTIONS OTHERS         = 0 ).
    IF l_nomes_un_sel = abap_on.
      <fs_puntuacio>-seleccio = abap_on.
      LOOP AT ct_puntuacio ASSIGNING FIELD-SYMBOL(<fs_puntuacio2>) WHERE z_codi_subapar_punt = <fs_puntuacio>-z_codi_subapar_punt
                                                                     AND z_sec               <> <fs_puntuacio>-z_sec.
        <fs_puntuacio2>-seleccio = abap_off.
      ENDLOOP.
      READ TABLE gs_dyn_0500-gt_punt_ofer_alv ASSIGNING FIELD-SYMBOL(<fs_punt_ofer_alv>) WITH KEY z_codi_crit_punt = <fs_puntuacio>-z_codi_crit_punt.
      IF sy-subrc = 0.
        <fs_punt_ofer_alv>-z_sec        = <fs_puntuacio>-z_sec.
        <fs_punt_ofer_alv>-z_punts      = <fs_puntuacio>-z_punt_max_do.
        <fs_punt_ofer_alv>-z_sec_resta  = <fs_puntuacio>-z_sec_resta.
      ENDIF.
    ENDIF.
  ENDMETHOD.

  METHOD check_change_seleccio.
    READ TABLE ct_puntuacio ASSIGNING FIELD-SYMBOL(<fs_puntuacio>) INDEX i_row_id-index.
    CHECK sy-subrc = 0.
    READ TABLE ct_puntuacio ASSIGNING FIELD-SYMBOL(<fs_punt_resta>) WITH KEY z_opcional = abap_on.
    IF sy-subrc = 0 AND ( <fs_punt_resta>-seleccio = abap_on OR ( <fs_puntuacio>-z_opcional = abap_on ) )
       AND <fs_puntuacio>-z_pun_rel_sel EQ abap_false.

      READ TABLE ct_puntuacio ASSIGNING FIELD-SYMBOL(<fs_punt_atual>) WITH KEY seleccio = abap_on.
      IF sy-subrc = 0.
        "Si aquest criteri té un subapartat com a opcional, es permet marcar 2 (un sent l'opcional) sempre i quan la suma dels dos criteris sigui > 0.
        DATA(l_puntuacio)                = <fs_puntuacio>-z_punt_max_do. "Recuperem puntuació seleccionada
        DATA(l_puntuacio_actual_marcada) = <fs_punt_atual>-z_punt_max_do. "Recuperem puntuació actualment marcada

**      >> INI MODIF ETODR SPEC-50706
*        IF ( l_puntuacio + l_puntuacio_actual_marcada ) LE 0.      " SPEC-49499 ETJMD 06.06.2018
        IF ( l_puntuacio + l_puntuacio_actual_marcada ) LT 0.
**      << FI MODIF ETODR SPEC-50706
          MESSAGE s555(zrm_errors) DISPLAY LIKE 'E'.   "La puntuació deu ser major a 0
          RAISE no_seleccionable.
        ENDIF.
        "Comprovem si s'ha seleccionat ara el Z_OPCIONAL o ja era el marcat
        IF <fs_puntuacio>-z_opcional IS NOT INITIAL.
*          e_result = abs( l_puntuacio_actual_marcada ) - abs( l_puntuacio ). ETLJS SPEC-47173
          e_result = abs( l_puntuacio_actual_marcada ) + l_puntuacio .
        ELSE.
*          e_result = abs( l_puntuacio ) - abs( l_puntuacio_actual_marcada ). ETLJS SPEC-47173
          e_result = abs( l_puntuacio ) + l_puntuacio_actual_marcada .
        ENDIF.

        IF e_result < 0.
          IF <fs_puntuacio>-z_opcional IS INITIAL.
            e_nomes_un_sel = abap_on.
          ELSE.
            RAISE no_seleccionable.
          ENDIF.
        ELSE.
          IF i_readonly <> abap_on AND <fs_puntuacio>-z_opcional = abap_false.
            <fs_puntuacio>-seleccio = abap_on.
          ELSEIF i_readonly <> abap_on AND <fs_puntuacio>-seleccio = abap_false.
            <fs_puntuacio>-seleccio = abap_on.
          ELSE.
            <fs_puntuacio>-seleccio = abap_false.
          ENDIF.
        ENDIF.
      ELSE.
        IF i_readonly <> abap_on.
          <fs_puntuacio>-seleccio = abap_off.
        ENDIF.
      ENDIF.
      DATA lv_cont        TYPE z_num_sel.

    ELSEIF sy-subrc EQ 0 AND <fs_puntuacio>-z_pun_rel_sel EQ abap_true.
      CLEAR lv_cont.
      CLEAR e_nomes_un_sel.
      IF <fs_puntuacio>-seleccio EQ abap_true.
        CLEAR <fs_puntuacio>-seleccio.
      ELSE.
        <fs_puntuacio>-seleccio = abap_true.
      ENDIF.
      IF <fs_puntuacio>-z_opcional EQ abap_true.
        LOOP AT ct_puntuacio ASSIGNING FIELD-SYMBOL(<fs_punt>) WHERE seleccio = abap_true.
          IF <fs_punt>-z_opcional EQ abap_false.
            CLEAR <fs_punt>-seleccio.
          ELSE.
            ADD 1 TO lv_cont.
          ENDIF.
        ENDLOOP.

        CHECK gt_punt_do IS NOT INITIAL AND <fs_puntuacio>-z_pun_rel_sel EQ abap_true.
        READ TABLE gt_punt_do ASSIGNING FIELD-SYMBOL(<fs_punt_do>) WITH KEY z_num_sel = lv_cont.
        IF sy-subrc EQ 0.
          e_result = <fs_punt_do>-z_punts.
        ENDIF.
      ELSE.
        LOOP AT ct_puntuacio ASSIGNING <fs_punt> WHERE seleccio = abap_true
                                                   AND z_opcional = abap_true.
          CLEAR <fs_punt>-seleccio.
        ENDLOOP.
        e_result = <fs_puntuacio>-z_punt_max_do.
      ENDIF.
    ELSE.
      e_nomes_un_sel = abap_on.
    ENDIF.
  ENDMETHOD.



  METHOD show_descripcio_llarga.
    DATA: lt_text                TYPE wcb_tdline_tab,
          wa_text                TYPE tdline,
          lt_tdline              TYPE TABLE OF tline,
          lt_wrap_text           TYPE string_table,
          lv_string_text(160000) TYPE c,
          lv_titol               TYPE sy-title,
          lv_index               TYPE n,
          lv_read_text           TYPE string.

    DATA(lv_sec) = is_puntuacio-z_sec.
    SHIFT lv_sec LEFT DELETING LEADING '0'.

    CONCATENATE 'Descripció completa de la puntuació' lv_sec 'del criteri' is_puntuacio-z_codi_crit_punt INTO lv_titol SEPARATED BY space.

    SELECT SINGLE z_descripcio_do AS text1, z_descripcio_do_2 AS text2, z_descripcio_do_3 AS text3, z_descripcio_do_4 AS text4
      FROM zrm_dm_suapar_do
      INTO @DATA(wa_descripcio)
      WHERE z_codi_subapar_punt = @is_puntuacio-z_codi_subapar_punt
        AND z_sec               = @is_puntuacio-z_sec.

    lv_index = 1.
    CLEAR: lv_string_text.
    DO.
      CONCATENATE 'text' lv_index INTO lv_read_text.
      ASSIGN COMPONENT lv_read_text OF STRUCTURE wa_descripcio TO FIELD-SYMBOL(<fs_text>).
      IF sy-subrc <> 0.
        EXIT.
      ELSE.
        IF lv_index EQ 1.
          CONCATENATE lv_string_text <fs_text> INTO lv_string_text.
        ELSE.
          CONCATENATE lv_string_text <fs_text> INTO lv_string_text SEPARATED BY space.
        ENDIF.
      ENDIF.
      ADD 1 TO lv_index.
    ENDDO.
    CHECK lv_string_text IS NOT INITIAL.
    CALL FUNCTION 'ZRM_EDIT_STRING'
      EXPORTING
        i_title = lv_titol
      CHANGING
        c_text  = lv_string_text.

  ENDMETHOD.

  METHOD handle_data_changed_0501.

  ENDMETHOD.

  METHOD handle_hotspot_click_0501.
    DATA: l_editable   TYPE xfeld,
          lt_dades_sub TYPE zrm_cl_valoracio_ofertes=>ty_t_puntuacio_subapartats.

    DATA: lv_tittle TYPE string,
          lv_text1  TYPE string,
          lv_text2  TYPE string.

    CHECK gs_dyn_0501-gv_alv_puntuacio IS BOUND.

    READ TABLE gs_dyn_0501-gt_puntuacio_ori ASSIGNING FIELD-SYMBOL(<fs_puntuacio>) INDEX e_row_id-index.
    IF sy-subrc = 0.
      IF e_column_id-fieldname EQ 'Z_DESCRIPCIO_DO' AND e_row_id-index IS NOT INITIAL.
        show_descripcio_llarga( is_puntuacio = <fs_puntuacio> ).
      ENDIF.
      IF e_column_id-fieldname EQ 'MOTIVACIO' AND e_row_id-index IS NOT INITIAL
        AND <fs_puntuacio>-z_motivacio IS BOUND.
        IF g_readonly IS INITIAL.
          l_editable = abap_on.
        ELSE.
          l_editable = abap_off.
        ENDIF.
        <fs_puntuacio>-z_motivacio->edit_text( EXPORTING i_editable = l_editable ).
      ENDIF.

      IF e_column_id-fieldname EQ 'COMENTARIS' AND e_row_id-index IS NOT INITIAL
        AND <fs_puntuacio>-z_comentaris IS BOUND.
        <fs_puntuacio>-z_comentaris->edit_text( EXPORTING i_editable = abap_on ).
      ENDIF.

      IF e_column_id-fieldname EQ 'SELECCIO' AND e_row_id-index IS NOT INITIAL.
        CALL METHOD gr_backend->get_punt_subapart
          IMPORTING
            et_dades_subapart = lt_dades_sub.

        READ TABLE gs_dyn_0501-gt_puntuacio_ori ASSIGNING FIELD-SYMBOL(<fs_punt>) INDEX e_row_id.
        IF sy-subrc EQ 0.
          IF <fs_punt>-z_codi_subapar_punt_dep IS NOT INITIAL.
            READ TABLE lt_dades_sub
             ASSIGNING FIELD-SYMBOL(<fs_dades_sub>)
                  WITH KEY z_codi_subapar_punt = <fs_punt>-z_codi_subapar_punt_dep
                           z_punts             = <fs_punt>-z_punts_no_val_dep
                           z_num_ofer          = <fs_punt>-z_num_ofer.

            IF sy-subrc EQ 0 AND <fs_punt>-z_punt_max_do IS NOT INITIAL.

              CONCATENATE 'El criteri' gs_dyn_0501-gv_crit_punt 'no es pot puntuar ' INTO lv_tittle SEPARATED BY space.
              lv_text1 = <fs_punt>-z_punt_max_do.
              CONCATENATE 'El criteri' gs_dyn_0501-gv_crit_punt 'no es pot puntuar amb' lv_text1 'ja que s’ha puntuat' INTO lv_text1 SEPARATED BY space.
              lv_text2 = <fs_dades_sub>-z_punts.
              CONCATENATE  'amb' lv_text2 'el criteri' <fs_dades_sub>-z_codi_crit_punt INTO lv_text2 SEPARATED BY space.

              CALL FUNCTION 'POPUP_TO_INFORM'
                EXPORTING
                  titel = lv_tittle
                  txt1  = lv_text1
                  txt2  = lv_text2.
              CHECK 1 = 2.
            ENDIF.
          ENDIF.

          IF <fs_punt>-z_codi_subapar_punt_dep2 IS NOT INITIAL.
            READ TABLE lt_dades_sub
                    ASSIGNING <fs_dades_sub>
                         WITH KEY z_codi_subapar_punt = <fs_punt>-z_codi_subapar_punt_dep2
                                  z_num_ofer          = <fs_punt>-z_num_ofer.

            IF sy-subrc EQ 0 AND <fs_dades_sub>-z_punts IS NOT INITIAL AND <fs_punt>-z_punt_max_do EQ <fs_punt>-z_punts_no_val_dep.

              CONCATENATE 'El criteri' gs_dyn_0501-gv_crit_punt 'no es pot puntuar ' INTO lv_tittle SEPARATED BY space.
              CONCATENATE 'El criteri' gs_dyn_0501-gv_crit_punt 'no es pot puntuar ja que s’ha puntuat' INTO lv_text1 SEPARATED BY space.
              lv_text2 = <fs_dades_sub>-z_punts.
              CONCATENATE  'amb' lv_text2 'el criteri' <fs_dades_sub>-z_codi_crit_punt INTO lv_text2 SEPARATED BY space.

              CALL FUNCTION 'POPUP_TO_INFORM'
                EXPORTING
                  titel = lv_tittle
                  txt1  = lv_text1
                  txt2  = lv_text2.
              CHECK 1 = 2.
*   El criteri &1 no es pot puntuar
            ENDIF.
          ENDIF.
        ENDIF.

        IF gr_backend->get_is_cp_iniciada( ) = abap_on.
          check_set_canvi_punt_dyn( CHANGING i_row_id        = e_row_id
                                             ct_puntuacio    = gs_dyn_0501-gt_puntuacio_ori ).
        ELSE.
          change_seleccio_puntuacio(  EXPORTING i_row_id        = e_row_id
                                      CHANGING  ct_puntuacio    = gs_dyn_0501-gt_puntuacio_ori ).
          gs_dyn_0501-gv_alv_puntuacio->refresh_table_display( ).
        ENDIF.
      ENDIF.
    ENDIF.
*    gv_ucomm = 'X'.
  ENDMETHOD.

  METHOD handle_toolbar_0501.
    DATA: ls_toolbar  TYPE stb_button.

    "append a separator to normal toolbar
    CLEAR ls_toolbar.
    MOVE 3 TO ls_toolbar-butn_type.
    APPEND ls_toolbar TO e_object->mt_toolbar.

    CLEAR ls_toolbar.
    MOVE 'BACK' TO ls_toolbar-function.
    MOVE icon_previous_object TO ls_toolbar-icon.
    MOVE ' ' TO ls_toolbar-disabled.
    APPEND ls_toolbar TO e_object->mt_toolbar.

    CLEAR ls_toolbar.
    MOVE 'NEXT' TO ls_toolbar-function.
    MOVE icon_next_object TO ls_toolbar-icon.
    MOVE ' ' TO ls_toolbar-disabled.
    APPEND ls_toolbar TO e_object->mt_toolbar.

  ENDMETHOD.

  METHOD handle_user_command_0501.
    DATA: lt_rows TYPE lvc_t_row.

    DATA: wa_subapartats TYPE gty_subapartats.

    DATA: lv_line TYPE z_sec,
          lv_save TYPE xfeld.
    FIELD-SYMBOLS: "<subapartats> LIKE LINE OF gs_dyn_0200-gt_subapartats,
                   <rows>        LIKE LINE OF lt_rows.

    CASE e_ucomm.
      WHEN 'BACK'.
        PERFORM check_before_puntuar  USING gs_dyn_0501-gt_puntuacio_ori
                                   CHANGING lv_save.

        IF lv_save EQ abap_true.
          READ TABLE gs_dyn_0501-gt_punt_ofer_alv TRANSPORTING NO FIELDS WITH KEY is_actual = abap_on.
          CHECK sy-subrc = 0.
          DATA(l_index) = sy-tabix.
          IF sy-tabix > 1.
            READ TABLE gs_dyn_0501-gt_punt_ofer_alv ASSIGNING FIELD-SYMBOL(<fs_back>) INDEX l_index - 1.
            CHECK sy-subrc = 0.
            READ TABLE gs_dyn_0501-gt_punt_ofer_alv ASSIGNING FIELD-SYMBOL(<fs_current>) WITH KEY is_actual = abap_on.
            CLEAR: <fs_current>-is_actual.

            <fs_back>-is_actual = abap_on.
          ENDIF.
        ENDIF.
      WHEN 'NEXT'.
        PERFORM check_before_puntuar  USING    gs_dyn_0501-gt_puntuacio_ori
                                      CHANGING lv_save.

        IF lv_save EQ abap_true.
          READ TABLE gs_dyn_0501-gt_punt_ofer_alv TRANSPORTING NO FIELDS WITH KEY is_actual = abap_on.
          CHECK sy-subrc = 0.
          l_index = sy-tabix.
          DESCRIBE TABLE gs_dyn_0501-gt_punt_ofer_alv LINES DATA(l_max).
          IF sy-tabix < l_max.
            READ TABLE gs_dyn_0501-gt_punt_ofer_alv ASSIGNING FIELD-SYMBOL(<fs_next>) INDEX l_index + 1.
            CHECK sy-subrc = 0.
            READ TABLE gs_dyn_0501-gt_punt_ofer_alv ASSIGNING <fs_current> WITH KEY is_actual = abap_on.
            CLEAR: <fs_current>-is_actual.

            <fs_next>-is_actual = abap_on.
          ENDIF.
        ENDIF.
    ENDCASE.
    PERFORM puntuar_0501 USING    gs_dyn_0501-gt_puntuacio_ori
                         CHANGING gs_dyn_0501-gt_puntuacio.

    PERFORM guardar_taules_rel USING gs_dyn_0501-gt_puntuacio_ori
                                     gs_dyn_0501-gs_punt_rec.

    PERFORM liberar_objetos_punt USING gs_dyn_0501-gv_alv_puntuacio
                                       gs_dyn_0501-gv_cont_puntuacio.
  ENDMETHOD.


  METHOD handle_toolbar_back.
    DATA: ls_toolbar  TYPE stb_button.

    CLEAR ls_toolbar.
    MOVE 3 TO ls_toolbar-butn_type.
    APPEND ls_toolbar TO e_object->mt_toolbar.

    CLEAR ls_toolbar.
    MOVE 'BACK' TO ls_toolbar-function.
    MOVE icon_previous_object TO ls_toolbar-icon.
    MOVE ' ' TO ls_toolbar-disabled.
    APPEND ls_toolbar TO e_object->mt_toolbar.

  ENDMETHOD.

  METHOD handle_toolbar_next.
    DATA: ls_toolbar  TYPE stb_button.

    CLEAR ls_toolbar.
    MOVE 3 TO ls_toolbar-butn_type.
    APPEND ls_toolbar TO e_object->mt_toolbar.

    CLEAR ls_toolbar.
    MOVE 'NEXT' TO ls_toolbar-function.
    MOVE icon_next_object TO ls_toolbar-icon.
    MOVE ' ' TO ls_toolbar-disabled.
    APPEND ls_toolbar TO e_object->mt_toolbar.

  ENDMETHOD.
ENDCLASS.